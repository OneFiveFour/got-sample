plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs.kotlin'
    id 'io.gitlab.arturbosch.detekt' version '1.21.0'
}

def signingProperties = new Properties()
def signingFile = file("../got.properties")
if (signingFile.exists()) {
    signingFile.withInputStream { signingProperties.load(it) }
}

android {
    namespace 'io.redandroid.gameofthrones'
    compileSdk 33

    defaultConfig {
        applicationId "io.redandroid.gameofthrones"
        minSdk 23
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    composeOptions {
        kotlinCompilerExtensionVersion = "1.3.1"
    }
    buildFeatures {
        viewBinding true
        dataBinding true
        compose = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }


    signingConfigs {
        release {
            if (signingFile.exists()) {
                storeFile file(signingProperties.getProperty("signing.keystorePath"))
                storePassword signingProperties.getProperty("signing.keystorePassword")
                keyAlias signingProperties.getProperty("signing.keyAlias")
                keyPassword signingProperties.getProperty("signing.keyPassword")
            }
        }
    }

    buildTypes {
        debug {
            debuggable true
        }

        release {
            minifyEnabled true
            debuggable false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation project(path: ':data')

    // Android Basics
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation "androidx.fragment:fragment-ktx:1.5.4"
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'

    // Compose
    implementation "androidx.compose.runtime:runtime:1.2.1"
    implementation "androidx.compose.ui:ui:1.2.1"
    implementation "androidx.compose.material:material:1.2.1"
    debugImplementation "androidx.compose.ui:ui-tooling:1.2.1"
    implementation "androidx.compose.ui:ui-tooling-preview:1.2.1"

    // Data Binding
    implementation 'androidx.databinding:databinding-common:8.0.0-alpha06'
    implementation 'androidx.databinding:databinding-adapters:8.0.0-alpha06'

    // Dependency Injection
    implementation "com.google.dagger:hilt-android:2.44"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    kapt 'com.google.dagger:hilt-android-compiler:2.44'

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:2.5.3"
    implementation "androidx.navigation:navigation-ui-ktx:2.5.3"

    // Paging
    implementation "androidx.paging:paging-runtime-ktx:3.1.1"

    // Fast Scroll
    implementation "io.github.l4digital:fastscroll:2.1.0"

    // Logging
    implementation 'com.jakewharton.timber:timber:5.0.1'

    // Unit Tests
    testImplementation 'junit:junit:4.13.2'

    // Instrumentation Tests
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}